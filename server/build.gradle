import java.text.SimpleDateFormat

plugins {
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}
apply plugin: 'os-package'
apply plugin: 'se.mtm.artifactory-rpm'

project.ext {
    dropwizardVersion = '0.8.2'
    rpmVersion = version - '-SNAPSHOT'
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-assets:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-views-mustache:' + dropwizardVersion
    compile('io.dropwizard:dropwizard-client:' + dropwizardVersion) {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    compile 'commons-io:commons-io:2.4'
    compile 'org.fusesource:sigar:1.6.4'
    compile 'com.jcabi:jcabi-manifests:1.1'

    testCompile project(':client')
    testCompile 'io.dropwizard:dropwizard-testing:' + dropwizardVersion
    testCompile 'org.seleniumhq.selenium:selenium-java:2.47.1'
}

sourceSets {
    acceptanceTest {
        java.srcDir file('src/acceptanceTest/java')
        resources.srcDir file('src/acceptanceTest/resources')
        compileClasspath += main.output + test.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }

    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += main.output + test.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task acceptanceTest(type: Test) {
    description = 'Runs the acceptance tests'
    group = 'verification'
    testClassesDir = sourceSets.acceptanceTest.output.classesDir
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    reports.junitXml.destination = "$buildDir/acceptanceTest-test-results"
    reports.html.destination = "$buildDir/reports/acceptanceTest"
    dependsOn(test)
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.junitXml.destination = "$buildDir/integrationTest-test-results"
    reports.html.destination = "$buildDir/reports/integrationTest"
    dependsOn(test)
}

check {
    dependsOn(integrationTest)
    dependsOn(acceptanceTest)
}

shadowJar {
    dependsOn 'build'
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    manifest {
        attributes 'Main-Class': 'se.mtm.speech.synthesis.Main',
                'Version': version,
                'Build-Date': new SimpleDateFormat("yyyy-MM-dd HH:mm").format(new Date())
    }
}

ospackage {
    packageName = 'speech-synthesis-server'
    version = rpmVersion
    release = (System.getenv().BUILD_NUMBER != null) ? System.getenv().BUILD_NUMBER : "0";
    os = LINUX
    user = "root"

    installUtils file('rpm/scripts/utils.sh')
    preInstall file('rpm/scripts/preInstall.sh')
    postInstall file('rpm/scripts/postInstall.sh')
    postUninstall file('rpm/scripts/postUninstall.sh')
    
    into '/opt/speech-synthesis-server'

    from('./build/libs') {
        into ('/opt/speech-synthesis-server/lib')
        fileMode = 0555
    }

    from('../configuration-prod.yaml') {
       into('/opt/speech-synthesis-server/conf')
       fileMode = 0755
    }

    from('./src/scripts') {
        into('/opt/speech-synthesis-server/scripts')
        fileMode = 0755
    }

    link('/etc/init.d/speech-synthesis-server', '/opt/speech-synthesis-server/scripts/speech-synthesis-server.sh')
}

buildRpm {
    it.dependsOn shadowJar
}

deployRpm {
    it.dependsOn buildRpm
}

plugins {
    id 'java'
    id 'jacoco'
    id 'pmd'
    id 'com.github.johnrengelman.shadow' version '1.2.1'
}

version = '1.0.0'

project.ext {
    dropwizardVersion = '0.8.1'
}

dependencies {
    compile 'io.dropwizard:dropwizard-core:' + dropwizardVersion
    compile 'io.dropwizard:dropwizard-client:' + dropwizardVersion

    testCompile "junit:junit:4.12"
    testCompile 'io.dropwizard:dropwizard-testing:' + dropwizardVersion
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += main.output + test.output + configurations.testRuntime
        runtimeClasspath += output + compileClasspath
    }
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests'
    group = 'verification'
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    reports.junitXml.destination = "$buildDir/integrationTest-test-results"
    reports.html.destination = "$buildDir/reports/integrationTest"
    dependsOn(test)
}

check {
    dependsOn(integrationTest)
}

shadowJar {
    mergeServiceFiles()
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    manifest {
        attributes 'Main-Class': 'se.mtm.speech.synthesis.Main',
                'Implementation-Version': version
    }
}

repositories {
    jcenter()
}

pmd {
    toolVersion = '5.1.3'
    ignoreFailures = true
    ruleSets = [
            'java-basic',
            'java-unusedcode'
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
